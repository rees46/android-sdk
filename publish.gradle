def pomConfig = {
	licenses {
		license {
			name "The Apache Software License, Version 2.0"
			url "http://www.apache.org/licenses/LICENSE-2.0.txt"
			distribution "repo"
		}
	}

	scm {
		connection "scm:git:github.com:rees46/rees46-android-sdk.git"
		developerConnection "scm:git:ssh://github.com:rees46/rees46-android-sdk.git"
		url "https://github.com:rees46/rees46-android-sdk/tree/master"
	}
}
def urls = [rees46: "https://github.com/rees46/rees46-android-sdk", personaclick: "https://personaclick.com"]

def publicationNames = []
publishing {
	publications {
		android.libraryVariants.all { variant ->
			if (variant.buildType.name == "debug") return // Prevents publishing debug library

			def flavored = !variant.flavorName.isEmpty()

			def variantArtifactId = flavored ? variant.flavorName.replace('_', '-') + "-sdk" : project.name

			def javaDocDestDir = file("${layout.buildDirectory.get().asFile}/docs/javadoc ${flavored ? variantArtifactId : ""}")

			def sourceDirs = variant.sourceSets.collect {
				it.javaDirectories
			}
			def javadoc = task("${variant.name}Javadoc", type: Javadoc) {
				description "Generates Javadoc for ${variant.name}."
				source = variant.javaCompileProvider.get().source
				destinationDir = javaDocDestDir
				classpath += files(android.getBootClasspath().join(File.pathSeparator))
				configurations.implementation.setCanBeResolved(true)
				classpath += files(configurations.implementation)
				options.links("http://docs.oracle.com/javase/7/docs/api/");
				options.links("http://d.android.com/reference/");
				exclude '**/BuildConfig.java'
				exclude '**/R.java'
				failOnError false
			}
			def javadocJar = task("${variant.name}JavadocJar", type: Jar, dependsOn: javadoc) {
				description "Puts Javadoc for ${variant.name} in a jar."
				archiveClassifier.set('javadoc-' + variant.name)
				from javadoc.destinationDir
			}
			def sourcesJar = task("${variant.name}SourcesJar", type: Jar) {
				description "Puts sources for ${variant.name} in a jar."
				archiveClassifier.set('sources-' + variant.name)
				from sourceDirs
			}

			def publicationName = "${variant.name.capitalize()}Library"
			publicationNames.add(publicationName)

			"$publicationName"(MavenPublication) {
				artifactId variantArtifactId
				groupId "com.${variant.flavorName}"
				version version

				artifact variant.packageLibraryProvider.get()
				artifact sourcesJar
				artifact javadocJar

				pom {
					packaging 'aar'
					withXml {
						def root = asNode()
						root.appendNode("name", variantArtifactId)
						root.appendNode("description", "${variant.flavorName.toUpperCase()} Android SDK")
						root.appendNode("url", urls[variant.flavorName])
						root.children().last() + pomConfig
						def depsNode = root["dependencies"][0] ?: root.appendNode("dependencies")

						def addDep = {
							if (it.group == null) return
							def dependencyNode = depsNode.appendNode('dependency')
							dependencyNode.appendNode('groupId', it.group)
							dependencyNode.appendNode('artifactId', it.name)
							dependencyNode.appendNode('version', it.version)
							if (it.hasProperty('optional') && it.optional) {
								dependencyNode.appendNode('optional', 'true')
							}
						}

						configurations.implementation.allDependencies.each addDep
						if (flavored) {
							configurations["${variant.flavorName}Implementation"].allDependencies.each addDep
						}
					}
				}
			}
		}
	}
	repositories {
		maven {
			name = "sonatype"

			def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
			def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
			url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

			credentials {
				username ossrhUsername
				password ossrhPassword
			}
		}
	}
}

def sonatypeIds = [rees46: sonatype_rees46, personaclick: sonatype_personaclick]

afterEvaluate {
	nexusPublishing {
		repositories {
			android.libraryVariants.all { variant ->
				if (variant.buildType.name == "debug") return

				"${variant.flavorName}Sonatype" {
					stagingProfileId = sonatypeIds[variant.flavorName]
					username = ossrhUsername
					password = ossrhPassword
					nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
					snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
				}
			}
		}
	}
}

ext["signing.keyId"] = signing_keyId
ext["signing.password"] = signing_password
ext["signing.secretKeyRingFile"] = signing_secretKeyRingFile
ext["ossrhUsername"] = ossrhUsername
ext["ossrhPassword"] = ossrhPassword

signing {
	sign publishing.publications
}
