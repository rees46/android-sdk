plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.personalization.demo'
    compileSdkVersion 34

    flavorDimensions += 'default'

    // Read shop.id from local.properties (check both root and demo-app directories)
    def localProperties = new Properties()
    def localPropertiesFile = rootProject.file('local.properties')
    if (!localPropertiesFile.exists()) {
        localPropertiesFile = project.file('local.properties')
    }
    if (localPropertiesFile.exists()) {
        localProperties.load(new FileInputStream(localPropertiesFile))
    }
    def shopId = localProperties.getProperty('shop.id', '357382bf66ac0ce2f1722677c59511')

    defaultConfig {
        applicationId "com.personalization.demo"
        minSdkVersion 19
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Add shop ID to BuildConfig
        buildConfigField "String", "SHOP_ID", "\"${shopId}\""
    }

    productFlavors {
        rees46 {
            dimension 'default'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }
}

dependencies {
    implementation project(':personalization-sdk')

    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.fragment:fragment-ktx:1.6.2'
    
    // Firebase
    implementation platform('com.google.firebase:firebase-bom:32.7.0')
    implementation 'com.google.firebase:firebase-messaging'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

