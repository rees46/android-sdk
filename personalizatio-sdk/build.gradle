plugins {
	id 'com.android.library'
	id 'org.jetbrains.kotlin.android'
	id 'maven-publish'
	id 'signing'
}

version='2.0.9'

android {
	compileSdkVersion 34
	flavorDimensions += 'default'

	defaultConfig {
		minSdkVersion 19
		targetSdkVersion 34
		buildConfigField "String", "VERSION_NAME", "\"" + version + "\""
		versionCode 1
		versionName "1.0"
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_17
		targetCompatibility JavaVersion.VERSION_17
	}

	kotlinOptions {
		jvmTarget = '17'
	}

	sourceSets {
		main {
			main.java.srcDirs = ['src/main/kotlin']
		}
	}

	productFlavors {
		rees46 {
			dimension 'default'
			ext.set("group", "com.rees46")
			ext.set("libraryName", "com.rees46.sdk")
			ext.set("webSite", "https://rees46.com")
			ext.set("vcsUrl", "https://github.com/rees46/rees46-android-sdk")
		}
		personaclick {
			dimension 'default'
			ext.set("group", "com.personaclick")
			ext.set("libraryName", "com.personaclick.sdk")
			ext.set("webSite", "https://personaclick.com")
			ext.set("vcsUrl", "https://github.com/PersonaClick/android-sdk")
		}
	}

	testFixtures {
		enable = false
	}

	namespace 'com.personalizatio'
}

configurations {
	rees46Debug
	rees46Release
	personaclickDebug
	personaclickRelease
}

dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar'])

	api platform('com.google.firebase:firebase-bom:32.7.0')
	api 'com.google.firebase:firebase-messaging:23.4.1'
	implementation 'androidx.recyclerview:recyclerview:1.3.2'
	implementation 'com.google.android.material:material:1.11.0'
	implementation 'org.jetbrains:annotations:24.1.0'
	implementation 'com.github.bumptech.glide:glide:4.16.0'
	implementation 'androidx.media3:media3-exoplayer:1.2.1'
	implementation 'androidx.media3:media3-ui:1.2.1'
	implementation 'androidx.core:core-ktx:1.13.1'
	implementation 'androidx.work:work-runtime-ktx:2.9.0'
	implementation 'com.google.code.gson:gson:2.11.0'
}

group = 'com.personalizatio'

tasks.register('sourcesJar', Jar) {
	archiveClassifier.set('sources')
	from android.sourceSets.main.java.srcDirs
}

tasks.register('javadoc', Javadoc) {
	source = fileTree(dir: 'src/main/kotlin', include: '**/*.kt')
	classpath += files("${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar")
	failOnError false
	options.encoding = 'UTF-8'
	options.charSet('UTF-8')
	options.links("http://docs.oracle.com/javase/7/docs/api/")
	options.links("http://d.android.com/reference/")
	destinationDir = file("${buildDir}/docs/javadoc")
}

tasks.register('javadocJar', Jar) {
	dependsOn javadoc
	archiveClassifier.set('javadoc')
	from javadoc.destinationDir
}

tasks.register('printVersionName') {
	doLast {
		println version
	}
}

tasks.register('increaseVersionCode') {
	doLast {
		def (major, minor, patch) = version.tokenize('.')
		major = major.toInteger()
		minor = minor.toInteger()
		patch = patch.toInteger()

		Integer newPatch = patch + 1

		String result = "$major.$minor.$newPatch"

		String s = buildFile.getText().replaceFirst("version='$version'", "version='$result'")

		buildFile.setText(s)
	}
}

artifacts {
	archives javadocJar
	archives sourcesJar
}

afterEvaluate {
	tasks.javadoc.classpath += files(android.libraryVariants.collect { variant ->
		variant.javaCompileProvider.get().classpath
	})
}
