apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

version='1.8.11'

android {
	compileSdk 34
	flavorDimensions += 'default'
//	publishNonDefault true

	defaultConfig {
		minSdkVersion 19
		targetSdkVersion 34
		buildConfigField "String", "VERSION_NAME", "\"" + version + "\""
		aarMetadata {
			minCompileSdk = 34
		}
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_17
		targetCompatibility JavaVersion.VERSION_17
	}

	productFlavors {
		rees46 {
			dimension = 'default'
			project.ext.set("group", "com.rees46")
			project.ext.set("libraryName", "com.rees46.sdk")
			project.ext.set("webSite", "https://rees46.com")
			project.ext.set("vcsUrl", "https://github.com/rees46/rees46-android-sdk")
		}
		personaclick {
			dimension = 'default'
			project.ext.set("group", "com.personaclick")
			project.ext.set("libraryName", "com.personaclick.sdk")
			project.ext.set("webSite", "https://personaclick.com")
			project.ext.set("vcsUrl", "https://personaclick.com")
		}
	}

	testFixtures {
		enable = false
	}

	namespace 'com.personalizatio'//expects a release variant
}

configurations {
	rees46Debug
	rees46Release
	personaclickDebug
	personaclickRelease
}

dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar'])

	api platform('com.google.firebase:firebase-bom:32.7.0')
	api 'com.google.firebase:firebase-messaging:23.4.1'
	implementation 'androidx.recyclerview:recyclerview:1.3.2'
	implementation 'com.google.android.material:material:1.11.0'
	implementation 'org.jetbrains:annotations:24.1.0'
	implementation 'com.github.bumptech.glide:glide:4.16.0'
	implementation 'androidx.media3:media3-exoplayer:1.2.1'
	implementation 'androidx.media3:media3-ui:1.2.1'
//	implementation 'androidx.palette:palette:1.0.0'
}

group='com.personalizatio'
apply from: '../publish.gradle' // M

task sourcesJar(type: Jar) {
	archiveClassifier.set('sources')
	from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
	source = android.sourceSets.main.java.srcDirs
	classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	archiveClassifier.set('javadoc')
	from javadoc.destinationDir
}

task printVersionName {
	println version
}

task increaseVersionCode {
	def (major, minor, patch) = version.tokenize('.')
	major = major.toInteger()
	minor = minor.toInteger()
	patch = patch.toInteger()

	Integer newPatch = patch + 1

	String result = "$major.$minor.$newPatch"

	String s = buildFile.getText().replaceFirst("version='$version'", "version='$result'")

	buildFile.setText(s)
}

artifacts {
	archives javadocJar
	archives sourcesJar
}